@REM This Source Code Form is subject to the terms of the Mozilla Public
@REM License, v. 2.0.  If a copy of the MPL was not distributed with this
@REM file, You can obtain one at http://mozilla.org/MPL/2.0/.
@REM
@REM Copyright 1997 - July 2008 CWI, August 2008 - 2019 MonetDB B.V.

@echo off

setlocal ENABLEEXTENSIONS
@REM https://www.python.org/dev/peps/pep-0514/
set KEY_NAME1="HKEY_LOCAL_MACHINE\SOFTWARE\Python\PythonCore\@Python3_VERSION_MAJOR@.@Python3_VERSION_MINOR@\InstallPath"
set KEY_NAME2="HKEY_CURRENT_USER\Software\Python\PythonCore\@Python3_VERSION_MAJOR@.@Python3_VERSION_MINOR@\InstallPath"
set KEY_NAME3="HKEY_LOCAL_MACHINE\SOFTWARE\Wow6432Node\Python\PythonCore\@Python3_VERSION_MAJOR@.@Python3_VERSION_MINOR@\InstallPath"
set VALUE_NAME=""

FOR /F "usebackq skip=2 tokens=1,2,*" %%A IN (`REG QUERY %KEY_NAME1% /v %VALUE_NAME% 2^>nul`) DO (
    set ValueName=%%A
    set ValueType=%%B
    set ValueValue=%%C
)
IF NOT defined ValueName (
    FOR /F "usebackq skip=2 tokens=1,2,*" %%A IN (`REG QUERY %KEY_NAME2% /v %VALUE_NAME% 2^>nul`) DO (
        set ValueName=%%A
        set ValueType=%%B
        set ValueValue=%%C
    )
)
IF NOT defined ValueName (
    FOR /F "usebackq skip=2 tokens=1,2,*" %%A IN (`REG QUERY %KEY_NAME3% /v %VALUE_NAME% 2^>nul`) DO (
        set ValueName=%%A
        set ValueType=%%B
        set ValueValue=%%C
    )
)

IF defined ValueName (
    set LOCALPYTHONHOME=%ValueValue%
    set LOCALPYTHONPATH=%ValueValue%Lib
)

IF defined LOCALPYTHONHOME (
    endlocal & (
        set PYTHONHOME=%LOCALPYTHONHOME%
        set PYTHONPATH=%LOCALPYTHONPATH%
        set MONETDBPYTHONUDF=embedded_py=3
        set "PATH=%LOCALPYTHONHOME%;%PATH%"
    )
) ELSE (
    @echo MonetDB/Python Disabled: Python @Python3_VERSION_MAJOR@.@Python3_VERSION_MINOR@ installation not found.
    endlocal
)
